;;; This is a REPL, a live coding environment.
;;; Place your cursor inside a form and press alt+e to see immediate results!

; Caveats:
; Make sure you don't have too many or too few parentheses in the file. That confuses the parser!
; Some of these examples depend on symbols defined above them. Evaluating them in order is 
; recommended.
; When in doubt: check your browser's console.
; If all else fails: reload this tab.



;;; Basic Types and Collections

; Schem has lists, of course. (Evaluating this throws an error. Find out below, why!)
(1 2 3)

; Schem also has vectors, like clojure. (Hit alt+e! This evaluates just fine.)
[4 5 6]

; Lists and vectors can contain values like numbers:
[-2 3.4]

; Strings:
["They look like this." "Escape sequences are a \"thing\". \nNow you know."]

; Symbols: 
[foo bar i-am-a-lispy-symbol]
; (They evaluate to whatever value they are bound to in their environment, since these symbols 
; are undefined, evaluating the expression results in an error. )

; Javascript Symbols:
[document.location.href]
; (If a symbol's name contains dots, their values aren't looked up in a Schem environment of 
; the interpreter, but in the javascript environment the interpreter is running in.
; Depending on where that is, you'll have access to the web API via "window.", 
; the browser API via "chrome." or "browser.", etc.)

; A short digression: Quoting symbols
; If you want to refer to the symbol itself (instead of the value associated with it) you'd 
; have to 'quote' it using either the 'quote' function or the apostrophe shorthand.
[(quote foo) 'bar 'i-am-a-lispy-symbol]

; Keywords: 
[:foo :moo :boo]
; (They evaluate to themselves. If a symbol is a name for something, a keyword would be a name by itself.)

; Maps are collections that work like dictionaries. They associate values with keys. 
{:eng "Hello!" :ger "Hallo!" :aut "Griaß Ihna!"}
; Keywords make very nice keys...

; ...but strings and numbers are valid keys, too! Putting commas between key value pairs makes
; maps easier to read to humans. You can put them anywhere, actually! The parser just ignores them.
{1 "one", "two" 2}

; And who could live without regular expressions? Here's one in action:
(re-find #"(.[ge]+).*(.x)\w*" "An irregular expression or something, i guess...")



;;; Reading LISP Forms

; When evaluating a form, the first element of a list is treated like a function. It gets called
; with the other form elements supplied as arguments. Evaluation starts at the innermost parens
; and works its way 'upwards' by substituting what's in between the parentheses with the return
; value of the function.
(* 7 (+ 3 2 1))

; So during evaluation the form (* 7 (+ 3 2 1)) turns into (* 7 6) which in turn turns into 42.

; Lisp uses prefix notation. Normal people would write the calculation like this: 
;   (7 * (3 + 2 + 1))
;
; And normal programmers might write it like this:
;   multiply(7, add(3, 2, 1));
;
; (Provided they defined the functions 'multiply' and 'add' somewhere. Btw: Notice how this has
; the same amount of parentheses but none of that comma-and-semicolon-stuff?



;;; Functions

; Functions are created using the 'fn' form.
(fn sqr "Multiplies a value with itself." (x) (* x x))
; (The name and docstring are both optional.)

; Most of the times, when you create a function, you'll want to bind it to some symbol so you 
; can re-use it later:
; (Notice that parameters can be defined in a [vector] instead of a (list), which looks nicer.)
(def add (fn [x y] (+ x y)))
 
; Now we can call 'add' with two numbers:
; (Granted, 'add' is a silly function, but it serves its purpose here.)
(add 1 2)

; You could also create and bind a function to a symbol in one go using the 'defn' macro:
; (This 'add' is still silly, but you had to type five characters less.)
(defn add [x y] (+ x y))

; Ooor! You could define the same thing using an anonymous function literal:
; (Functions created this way can have up to nine parameters named %1 to %9.
; ...and "%" is synonymous with "%1" if you want to save one more keypress.)
(def add #(+ %1 %2))

; Now that's eight characters less than in the first 'add' example!

; You might possibly spend more time contemplating how these work than you'll ever save
; by not having to type a few more characters.
; Then again, function literals are super handy for one-off functionality!

; For instance, the 'map' function takes another function and a sequence. Then it returns 
; a new sequence containing the results of 'applying' the function to each element:
(map #(* 2 %) [1 2 3])

; Variadic functions are also supported in Schem.
; In this example, "prefix" is bound to the first argument when calling "jazz-it-up",
; all following arguments are passed (as elements of a list) to "all-other-arguments".
; So the "&" works more or less like the rest operator ("...") in javascript.
(do
  (defn jazz-it-up [prefix & all-other-arguments]
    (map #(str prefix " " % "!") all-other-arguments))
  (jazz-it-up "And a" "one" "two" "three"))



;;; Fun with collections: map, filter & reduce

; 'map' was discussed above and it's a variadic function which were discussed above.
; See what happens when you call it with a function, followed by more than one sequence:
(map #(str %1 %2 %3) ["a" "b" "c"] [1 2 3] ["A" "B" "C"])

; And since we're talking about 'map', you can also 'filter' a list or vector using a predicate:
(defn even? "returns true if x divided by two is zero" [x] (= (rem x 2) 0))
(filter even? [1 2 3 4 5 6])

; Of course, 'map' and 'filter' wouldn't be complete without 'reduce':
(reduce * [1 2 3 7])

; Reduce takes an optional value to initialize the operation with:
(reduce + 1 [2 3])

; The function supplied to reduce should take two arguments:
(reduce #(str %1 ", " %2) [1 2 3])



;;; More fun with collections: conj, cons & concat

; (conj collection x & more)
; Conjoins elements onto a collection. 
(conj [1 2] 3 4)

; Beware: Even though Schem's implementation of Lists and Vectors rely on fakery and a vague commitment
; to "do it properly later", conj acts as if Lists were linked lists and conjoins new elements at the start.
(conj '(3 4) 1 2)

; You can also 'conj' maps or even vectors of alternating keys and values onto a map.
(conj {:a "a" :b "b"} {:b "B" :c "c"} [:d "d" :e "e"])

; (cons x collection)
; Constructs a new list from one element and a collection. It always adds the element at the beginning.
(cons 1 [2 3])
(cons 1 '(2 3))

; (concat collection & moreCollections)
; Concatenates the contents of collections into a single list.
(concat [1 2] [3] '(4 5))

; It also takes maps, which get converted into arrays of key-value-pairs...
(concat {:6 "6" :7 "7"} {:uwotm "8"})

; ... or nested collections, which get flattened by one level:
(concat ["outer" ["inner"] "outer"] ["other"])



;;; Asynchronous Evaluation & Concurrency

; The Schem interpreter makes heavy use of ES6 promises. Because of this, each evaluation step may take
; as much time as it likes to return a value.

; To see this in actino, we need to write some seriously slow code.
; The 'sleep' function does nothing and returns nil after n milliseconds:
(do
  (console-log "Breathe in.")
  (sleep 2000)
  (console-log "Breathe out."))

; Let's define a function that introduces an artificial delay to the evaluation of another function:
(defn delay-evaluation [ms, function] 
  (do
    (sleep ms)
    function))

; Using it, we can create a really laggy "multiply by two" function:
(defn slowly-multiply-by-two [x] (delay-evaluation 500 (#(* 2 %) x)))

; Now doubling one number takes half a second:
(slowly-multiply-by-two 6)

; So how long does it take to double each number in a vector with ten elements?
; Half a second. Because concurrency.
(map slowly-multiply-by-two [1 2 3 4 5 6 7 8 9 10])

; As you saw, 'map' evaluates each element more or less simultaneously. 
; 'filter' does the same with its predicates. So it should return a the whole filtered list about 
; as soon as the slowest predicate returs, regardless of the list's length.
(filter (delay-evaluation 500 #(= (rem % 2) 0)) [1 2 3 4 5 6 7 8 9 10])

; *) Technically, the functions won't actually run "in parallel" as in "on multiple threads".
; (Allthough, sometimes they might. It depends...)

; I don't expect anyone to write computationally expensive stuff in Schem; that would be a terrible idea.
; Still, these quirks should be useful when dealing with asynchronous javascript functions.

; Most importantly: if you were sending http requests in the 'map' or 'filter' functions, 
; those would magically happen simultaneously! Which is pretty cool, I think. 
; Just limit yourself to either short lists or forgiving web servers. ;)



;;; Macros

; ...are functions that can transform your code 'before' it gets evaluated. This allows you to
; change the language's syntax. So meta!
(defmacro executePostfix (fn [& l] (cons (last l) (butlast l))))

; The macro defined above turns a list (a b c ... f) into (f a b c ...) which then can be evaluated.
(executePostfix 1 2 3 4 +)

; If you want to see the transformed code itself instead of its result, you can 'expand' macros.
(macroexpand (executePostfix 1 2 +))

; 'macroexpand' does this for one level while 'macroexpand-all' will work even with nested macros.
(macroexpand-all (executePostfix 1 2 3 (executePostfix 3 4 +) *))

; Some core features are actually just macros. Want to know what these function literals 
; actually turn into? Eval' this:
(macroexpand #(+ %1 %2))



;;; Atoms

; ...can hold a value. They are kind of like having a variable without a name, which isn't
; too useful, even in contrived examples. So we create an atom and bind it to the symbol 'a':
(def a (atom 2))

; Atoms can be dereferenced to return their value.
(deref a)
; You can set their value to something entirely new,
(set! a 4)
; ...or 'swap' it out with the result of a function that got called with their current value.
(defn increment [x] (+ x 1))
(swap! a increment) ; <- eval' this repeatedly!

; Atoms support watches. These are functions that get called when the atom's value changes.
; (Evaluate the next line, then try evaluating the swap! example again and look at your console.)
(add-watch a :alerter (fn [key atom old-value new-value] (window.alert (str old-value "->" new-value))))

; An atom can have multiple watches, as long as each one was added with a unique key. 
; (Unique 'within' that atom, not globally.)
; If you know the key of a particular watch, you can also remove it, if you happen to grow weary of it.
(remove-watch a :alerter)



;;; Destructuring

; Every operation that binds values to symbols in an environment (like 'let' and 'fn') 
; supports sequential destructuring and basic associative destructuring.

; Here's a function that takes a vector of length three. Inside it, the variables x, y and z are 
; bound to the first, second and third element of the parameter it was called with, respectively:

(defn format-3d-coordinates [[x y z]]
    (str "x: " x ", y: " y ", z: " z))

(format-3d-coordinates [1 2 3])

; Actually, I lied. It takes not only vectors, but anything sequential structure that is indexable:
(format-3d-coordinates (list 4 5 6))

; Even things that don't fit into your computer's memory:
(def all-uneven-numbers (lazy-vector #(+ 1 (* 2 %))))
(format-3d-coordinates all-uneven-numbers)

; The same mechanism that drives variadic function definitions also can be used in 'let' forms.
; In this example, all elements after 'second' get shoved into a list which is bound to 'rest'.
(let [[first second & rest] (subvec all-uneven-numbers 0 5)]
  [first second rest])

; Nesting is allowed, too:
(do 
  (defn heady-plotter [[[x1 y1] [x2 y2]]]
    (str "Imagine a line going from " x1 "," y1 " to " x2 "," y2 "!"))

  (heady-plotter [[1  2] [3  4]]))

; We've seen how sequential destructuring matches the shapes of collections.
; Associative destructuring, on the other hand, looks up keys in maps:

(let [bauz 
        {:col "yellow", :sp "daug", :verb "wagging it's tail", :name "Beaiengeo"}
      mauz 
        {:col "black", :sp "cat", :verb "purring"}
      describe 
        (fn [{species :sp,
              color :col,
              verb :verb}]
          (str "The " color " " species " is " verb "."))]

  (str (describe bauz) (describe mauz)))

; Fetching values from nested maps is not supported for the time being, but you can combine 
; sequential and associative destructuring; and the '&' operator is legal at any level of nesting.


;;; Javascript interoperability

; Symbols that contain a dot get treated *differently* from regular Schem symbols. Instead of their
; value being looked up in the current Schem environment, they refer to global javascript objects.
; Schem uses its own types instead of plain js values and objects. 
; Sometimes, they get converted automatically; sometimes you'll have to do that using 'js->schem' & 'schem->js'
; This is as much a WIP as anything else...

;; JS-Symbols

; Invoking a js function:
(window.alert "This works.")

; Aliasing a js function:
; (notice the single quote)
(let (anoy 'window.alert)
  (anoy "This works too. Wohoo!"))

; Combining js function invocation and js property resolution:
(window.alert document.title)

;; Getting Stuff

; Getting a js property via the property accessor special form "(.somePropertyName js-object)"
(.title document)

; Property cains are resolved automatically, so there's no need for nested forms:
(.title.length document)

; ...which does the same as:
(.length document.title)

; This accessor form is just syntactic sugar for calling the 'dot' function:
; (notice how "title" has to be quoted)
(. document 'title)

; Sometimes, calling the 'dot' function directly might be useful:
; (It also takes strings in lieu of symbols.)
(do 
  (defn stringify-document-property [p] (str (. document p)))
  (def interesting-properties ['title 'location.hostname "body.innerHTML.length"])
  (map stringify-document-property interesting-properties))

;; Invokation using the 'dot' accessor form
; (.functionName javascript-object & arguments)

; If the property you're trying to access is a js function, it gets invoked immediately. (Instead of the function itself being returned.)
; This is inconsistent and ambiguous but it makes function chaining possible (using the '->' operator) 
; It also drastically reduces the number of parentheses you have to look at. 
(.alert window "mhkay")
(.toUpperCase document.URL)

; If you really, really need to retrieve a javascript function instead of calling it, use the 'dot' function:
(. window 'alert)

;; Setting Stuff

; The property setter special form works analogous to the getter form. 
; (!propertyName js-object newValue)

; Changing the tab's title:
(!title document "Schmäh Editor")

; Polluting the global namespace with a Schem Function that will magically turn into a javascript function.
(!albert window (fn [msg] (window.alert (.toUpperCase (schem->js msg)))))
; Now open your browser console and try running the following -> albert("Hello?")

; CAVEAT:
; These functions will always return an ES6 Promise due to the interpreter's asynchronous nature!

; And then there's the set function if you have to be explicit about things:
; (notice that the js symbol is quoted!)
(set! 'document.title "ᕕ( ՞ ᗜ ՞ )ᕗ")

; Yet another way of getting a js property that I don't feel like documenting properly:
(js-deref (js-ref document.location "href.length"))

; Explicit interop type conversion:
; (back and forth, just for illustrative purposes)
(js->schem (schem->js {:a 42 :b ["string" "thing"]}) {:arrays-to-vectors true :depth 9001})
(js->schem (schem->js {:v [1 2 3] :l '(4 5 6)}) {:depth 1})


;;; Foreign Execution Contexts (is that a good name?)

; These probably deserve another paragraph of documentation.    ¯\_( ツ )_/¯

;; The general concept

; 1) Defining an execution context that matches any regular tab:
(defcontext anyTab: {:tabQuery {:url "*://*/*"}})

; 2a) Evaluating and printing something in a different context (check any other tab's console).
; Foreign contexts are lazily realized by forms like this. The results from each context will 
; be returned in a list. Errors are silently swept under the rug, currently. Sometimes.
; Check your js console(s), don't forget about the background page.
(anyTab:(prn (+ 41 1)))

; 2b) Returning a value gathered from a different context in this context:
(str "They feel: " (anyTab: (prompt "How's it going over there?")))

;; Remote DOM manipulation and other shenanigans.

; 1) Opening a tab by calling a function of the browser API function: 
; (this only works because the editor lives in a privileged context)
; ((replace "chrome" with "browser" in other browsers.))
(chrome.tabs.create {:url "http://xkcd.com/297/"})

; 2) Defining a context with specific features: 
; (instead of relying on on-demand feature aggregation)
(defcontext xkcd: { :tabQuery {:url "*://*.xkcd.com/*"}
                    ; 'dom-manipulation' and 'tiny-reply' both depend on 'schem-interpreter'
                    ; the order in which they will be injected does matter
                    :features ["schem-interpreter" "dom-manipulation" "tiny-repl" "shlukerts"]})

; 3) Adding an event listener in foreign contexts: 
; (and doing something useless with local state)
(xkcd:
  (do
    (def count (atom 0))
    (add-listener "click" "#ctitle"
      '(do
        (swap! count #(+ 1 %)
          (window.alert (str "You clicked the title " (deref count) " times.")))))))

; 4) Increase the page's general comicness:
(xkcd:(set-css-text "body" "font-family: \"Comic Sans MS\", \"Comic Sans\", cursive"))

; 5) Opening the tiny repl (or Schem shell?) in the contexts:
; (The tiny reply allows you to evaluate Schem code within the respective local context.)
(xkcd:(tiny-repl-show))

; 6) Now try running this ↓ in the tiny repl:
; (set-css-text "body" "background-color: gray")

; 7,8) Crummy examples for using JSReferences to *do things elsewhere*:
(xkcd: ((js-ref (window.document.querySelector "#ctitle") "append") (shluk [:div "Y'ello!"])))
(xkcd: (set! (js-ref (window.document.querySelector "title") "text") "Y'orange!"))

; Another short digression on quoting:
; You've seen regular quoting above. There's also quasiquoting which is very useful in the context of
; foreign execution contexts. It allows you to unquote parts of of a quasiquoted expression.
; Because nobody wants to see so many "qua"s and "quo"s, there's this handy reader macro:
; (quasiquote (x (unquote y)) is the same as `(x ~y)

(let [x 1 y 2]
  (= 
     (quasiquote (x (unquote y)))
    `(x ~y)))

; You can also splice-unquote inside of quasiquoted forms using "~@". It evaluates to the contents of a collection.
; Here's a short example that illustrates all supported forms of quoting:

(let [onetwo '(1 2)]
  `[onetwo ~onetwo ~@onetwo])

; Now back to remote DOM manipulation!

; 9) Dynamically creating expressions for foreign contexts
; When you pass a quasiquoted form to a foreign context, you get the chance to partially evaluate it
; in "this" context, before the resulting expression is evaluated in the "other" context.
;
; In this example "new-title" is not bound to any value in the xkcd-context, the argument passed to
; "set-title" is only available in the editor context. By quasiquoting the whole form and unquoting 
; the symbol "new-title", we create can write a reusable function that lets us change the comic title
(defn set-title [new-title] 
    (xkcd: `(set! (js-ref (window.document.querySelector "#ctitle") "innerText") ~new-title)))

(set-title "Lithp Cycles")
(set-title "I wonder if the puns will continue forever.")


;;; Persistent contexts

; These are a draft / proof of concept for how you'd use golem to automatically 
; *do things* with specific web pages.

; Open any number of tabs, run this script. Now reload any of the web pages, follow links 
; or navigate to a different page. → A local "happyTab" context is automatically prepared whenever
; a tab is updated. Within that context, the :init form is called once the page completed loading.

(do
  (def beautify '(set-css-text "body" "font-family: \"Comic Sans MS\", \"Comic Sans\", cursive; background-color: hotpink"))
  (defcontext happyTab: {:tabQuery {:url "*://*/*"}
                         :life-time "persistent"
                         :features ["schem-interpreter" "dom-manipulation"]
                         :init beautify})
  (happyTab:(prn "Brace yourself!")))

;;; Autoinstantiate contexts

; These will finally let you do, what a user script manager is supposed to let you do: 
; Change the looks or behaviour of a web page "permanently"

; They can only be registered in your .golemrc file (which can't be written or read from the virtual file system)
; You can edit your rc-file from the options window, or just open it in the editor like a regular file.

; Paste the five lines below into your .golemrc and uncomment them. (Tip: select them and press [ctrl+k], [ctrl+u])

; (clear-autoinstantiate-context)
; (defcontext xkcd: { :tabQuery {:url "*://*.xkcd.com/*"}
;                     :init '(load-file "xkcd")
;                     :features ["schem-interpreter" "dom-manipulation" "tiny-repl" "shlukerts"]})
; (add-autoinstantiate-context xkcd:)

; When you save the .golemrc file, you are asked whether you want to execute its contents immediately. Click "ok" to do so.

; Your new rc-file registers a context in the autoinstantiate list. This means golem will automatically create the xkcd: context
; whenever a page is loaded that matches it's :url parameter in :tabQuery. Then, according to the context's :init parameter, 
; it will load a file named "xkcd" and locally execute its contents. We still need that "xkcd" file, though.

; So open a new editor window, paste (and uncomment) the following script into it. Then save [ctrl+s] the file as "xkcd":

; (set-css-text "body" "font-family: \"Comic Sans MS\", \"Comic Sans\", cursive")
; (prn "Increased the comicness!")

; Notice how, the next time you open xkcd.com in any tab, the page's font changes after it finishes loading.
; Since the .golemrc script is executed every time you start your browser, your modification will be active until you remove
; the context from the autoinstantiate list. 

;;; XML support

; Requesting an xml document via http and turning it into a native map.
(xml->map (xhr-get "http://dbpedia.org/data/Golem.rdf"))

; If you're absolutely positive that all node and attribute names would be valid keywords (i.e. they don't contain colons or slashes and such)
; then requesting a map with keyword keys will work too (otherwise :keyType defaults to "string").
(xml->map 
  (str "<doc>"
        "<node1>woo</node1>"
        "<node2 someAttribute=\"coo.\" someOtherAttribute=\"coo.\">hoo</node2>"
       "</doc>")
  {:keyType "keyword"})



;;; Some more examples for good measure

; 1) Implementing fizz-buzz using a lazy vector: (very lispy)
(defn fizz-buzz (start end)
  ; Subvec realizes a range of values in a lazy-vector, memoizing the values.
  ; This would save time if you needed to access an element more than once and if accessing it was expensive,
  ; e.g. if the accessor function contained a get request.
  ; Truth be told, I don't know if that's useful. But i thought lazy-vectors looked like fun.
  (subvec 
    (lazy-vector 
      #(cond 
        (= 0 (rem % 15)) "fizzbuzz" 
        (= 0 (rem % 3)) "fizz" 
        (= 0 (rem % 5)) "buzz"
        :else %)) 
    start end))

(fizz-buzz 0 100)

; 2) Turn the output of fizz-buzz into a div containing an unordered list (creating DOM nodes, not HTML code)
(let
  [to-li #(vector :li (str %))]
  (console-log 
    (shluk 
      [:ul 
        (map to-li (fizz-buzz 0 10))]))) 

; Tail call optimization: (if it wasn't for TCO, calling sum with high values would lead to stack overflows)
(do
  (def sum (fn (n acc)
            (if (= n 0)
              acc
              (sum (- n 1) (+ n acc)))))

  (sum 4141 0))



;;; Local storage

; Golem has its own virtual file system that uses the browser's local storage API.

; Storing and retrieving objects in the virtual file system:
(storage-create "/folder/subfolder/objectName" "an invaluable value")
(storage-read "/folder/subfolder/objectName")

; Show some bare vfs info: (implementing a dir/ls function is left as an exercise for the reader)
(storage-get-vfstree)

; Saving and loading scripts in the editor is possible via the command pallette <F1> or via shortcuts (<ctrl+s> & <ctrl+o>)
; But you can also access some editor functions from within the editor.
(editor-save-script "someCode")

; You could now load/bookmark this script by adding #someCode to the editor url and reloading the page.
; Or you could evaluate this:
(editor-load-script "someCode")
